
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.СтоимостьТоваров.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ПриобретениеТоваровТовары.Номенклатура КАК Номенклатура,
	|	СУММА(ПриобретениеТоваровТовары.Количество) КАК Количество,
	|	СУММА(ПриобретениеТоваровТовары.Сумма) КАК Стоимость
	|ИЗ
	|	Документ.ПриобретениеТоваров.Товары КАК ПриобретениеТоваровТовары
	|ГДЕ
	|	ПриобретениеТоваровТовары.Ссылка = &Ссылка
	|	И ПриобретениеТоваровТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипНоменклатуры.Товар)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПриобретениеТоваровТовары.Номенклатура";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение 				= Движения.ТоварыНаСкладах.Добавить();
		Движение.ВидДвижения 	= ВидДвиженияНакопления.Приход;
		Движение.Период 		= Дата;
		Движение.Склад 			= Склад;
		Движение.Номенклатура 	= Выборка.Номенклатура;
		Движение.Количество 	= Выборка.Количество;
		
		Движение 				= Движения.СтоимостьТоваров.Добавить();
		Движение.ВидДвижения 	= ВидДвиженияНакопления.Приход;
		Движение.Период 		= Дата;
		Движение.Номенклатура 	= Выборка.Номенклатура;
		Движение.Количество 	= Выборка.Количество;
		Движение.Стоимость 		= Выборка.Стоимость;
		
	КонецЦикла;
	
	
КонецПроцедуры
